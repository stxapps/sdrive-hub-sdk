apply plugin: 'com.android.library'

group = 'org.blockstack.reactnative'
version = '0.1.0'

def expoModulesCorePlugin = new File(project(":expo-modules-core").projectDir.absolutePath, "ExpoModulesCorePlugin.gradle")
apply from: expoModulesCorePlugin
applyKotlinExpoModulesCorePlugin()
useCoreDependencies()
useExpoPublishing()

// If you want to use the managed Android SDK versions from expo-modules-core, set this to true.
// The Android SDK versions will be bumped from time to time in SDK releases and may introduce breaking changes in your module code.
// Most of the time, you may like to manage the Android SDK versions yourself.
def useManagedAndroidSdkVersions = false
if (useManagedAndroidSdkVersions) {
  useDefaultAndroidSdkVersions()
} else {
  buildscript {
    // Simple helper that allows the root project to override versions declared by this library.
    ext.safeExtGet = { prop, fallback ->
      rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
    }
  }
  project.android {
    compileSdkVersion safeExtGet("compileSdkVersion", 36)
    defaultConfig {
      minSdkVersion safeExtGet("minSdkVersion", 30)
      targetSdkVersion safeExtGet("targetSdkVersion", 36)
    }
  }
}

android {
  namespace "org.blockstack.reactnative"
  defaultConfig {
    versionCode 1
    versionName "0.1.0"
  }
  lintOptions {
    abortOnError false
  }
}

dependencies {
  implementation 'com.squareup.okhttp3:okhttp:5.3.2'
  implementation 'com.nimbusds:nimbus-jose-jwt:10.6'
  implementation 'org.bouncycastle:bcprov-jdk18on:1.83'

  implementation "com.github.komputing.kethereum:model:0.83.0"
  implementation "com.github.komputing.kethereum:crypto:0.83.0"
  implementation "com.github.komputing.kethereum:extensions_kotlin:0.83.0"
  implementation "com.github.komputing.kethereum:crypto_api:0.83.0"
  implementation("com.github.komputing.kethereum:crypto_impl_bouncycastle:0.83.0") {
    exclude group: 'org.bouncycastle', module: 'bcprov-jdk15on'
  }
  implementation "com.github.komputing.khex:extensions:1.0.0"
  implementation "com.github.komputing:kbase58:0.1"
  implementation "com.github.komputing.khash:ripemd160:1.0.0-RC5"
  implementation "com.github.komputing.khash:sha256:1.0.0-RC5"
}
